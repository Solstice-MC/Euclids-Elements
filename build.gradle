plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
}

base.archivesName = mod_id
group = mod_group
version = mod_version

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
		resources.srcDirs += [ 'src/testmod/generated' ]
	}
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings:v2"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

	testmodImplementation sourceSets.main.output
}

fabricApi {
	configureDataGeneration {
		outputDirectory = file("src/testmod/generated")
		addToResources = false
		strictValidation = true
	}
}

runDatagen {
	outputs.dir("src/testmod/generated")
}

loom {
	accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")
	runs {
		client {
			source sourceSets.testmod
			name "Minecraft Client"
		}
		server {
			source sourceSets.testmod
			name "Minecraft Server"
		}
		datagen {
			source sourceSets.testmod
			name "Data Generation"
			ideConfigGenerated = true
		}
	}
	createRemapConfigurations(sourceSets.testmod)
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	filesMatching("fabric.mod.json") {
		expand project.properties
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId "euclids-elements"
			from components.java
		}
	}
}
tasks.build.finalizedBy publishToMavenLocal
