plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
}

base.archivesName = mod_id
group = mod_group
version = mod_version

repositories {
	maven { url 'https://maven.pkg.github.com/Solstice-mc' }
    maven { url 'https://maven.neoforged.net/releases' }
}

dependencies {
    minecraft "net.minecraft:minecraft:$minecraft_version"
    mappings loom.layered {
        it.mappings("net.fabricmc:yarn:$yarn_mappings:v2")
        it.mappings("dev.architectury:yarn-mappings-patch-neoforge:$yarn_mappings_patch_version")
    }
    neoForge "net.neoforged:neoforge:$neoforge_version"
}

loom {
    interfaceInjection {
        enableDependencyInterfaceInjection = true
    }
}

processResources {
    filesMatching('META-INF/neoforge.mods.toml') {
        expand project.properties
    }
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url = "https://maven.pkg.github.com/Solstice-MC/Euclids-Elements"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}

tasks.build.finalizedBy publishToMavenLocal
