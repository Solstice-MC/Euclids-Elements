plugins {
	id 'java'
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
}

apply from: "common.gradle"
apply from: "functions.gradle"

base.archivesName = mod_id
version = mod_version
group = mod_group

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
		resources.srcDirs += [ 'src/testmod/generated' ]
	}
}

loom {
	runs {
		client {
			inherit client
			source sourceSets.testmod
			name "Minecraft Client"
		}
		server {
			inherit server
			source sourceSets.testmod
			name "Minecraft Server"
		}
	}
	enableTransitiveAccessWideners = true
}

fabricApi {
	configureDataGeneration {
		outputDirectory = file("src/testmod/generated")
		createSourceSet = false
		addToResources = false
		strictValidation = true
	}
}

dependencies {
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	subprojects.each {
		api project(path: "${it.path}", configuration: "namedElements")
		include project("${it.path}")
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	withJavadocJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = 'euclids-elements'
		}
	}
}
